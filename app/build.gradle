plugins {
    id 'io.gitlab.arturbosch.detekt' version '1.0.0.RC8'
    id "org.jlleitschuh.gradle.ktlint" version "5.0.0"
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'jacoco-android'
apply plugin: 'com.novoda.static-analysis'

jacoco {
    toolVersion = "0.8.2"
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "net.noworks.android"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        manifestPlaceholders = [applicationLabel:"遊び場"]
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        develop {
            storeFile file("../key/develop.jks")
            storePassword "password"
            keyAlias "develop"
            keyPassword "password"
        }
    }
    buildTypes {
        debug {
            manifestPlaceholders = [applicationLabel: "遊び場(debug)"]
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            minifyEnabled false
            testCoverageEnabled true
            signingConfig signingConfigs.develop
        }
        release {
            manifestPlaceholders = [applicationLabel:"遊び場(release)"]
            minifyEnabled false
            versionNameSuffix '-RELEASE'
            //shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
    packagingOptions {
        // NOTE: commons 関連の jar で「duplicate files during packaging of APK」のエラーが出るため記述
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }
    testOptions {
        // NOTE FragmentUtilsTest のテスト実行時に Method getInstrumentation in android.test.InstrumentationTestCase not mocked という
        // NOTE エラーが出てしまうので追加
        // NOTE (See https://sites.google.com/a/android.com/tools/tech-docs/unit-testing-support for details)
        unitTests.returnDefaultValues = true
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            def versionName = variant.versionName
            def date = new java.text.SimpleDateFormat("yyyyMMdd_HHmmss").format(new Date())
            def newName = "${rootProject.name}_v${versionName}_${date}.apk"
            outputFileName = newName
        }
    }
}


dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
    skipEmptyPackages = true
    skipDeprecated = false
    noStdlibLink = false
}

jacocoAndroidUnitTestReport {
    csv.enabled false
    html.enabled true
    xml.enabled true
}

staticAnalysis {

    penalty none

    checkstyle {
        toolVersion '8.8'
        exclude project.fileTree('src/test/java')
        configFile rootProject.file('checkstyle-modules.xml')
        includeVariants { variant -> variant.name.contains('debug') }
    }

    pmd {
        toolVersion '6.8.0'
        exclude project.fileTree('src/test/java')
        ruleSetFiles = rootProject.files('pmd-rules.xml')
        ruleSets = [] // https://stackoverflow.com/questions/32247190/pmd-exclude-pattern-with-gradle
        ignoreFailures true
        includeVariants { variant -> variant.name.contains('debug') }
    }

    findbugs {
        toolVersion '3.0.1'
        excludeFilter rootProject.file('findbugs-excludes.xml')
        includeVariants { variant -> variant.name.contains('debug') }
    }

    lintOptions {
        lintConfig rootProject.file('lint-config.xml')
        checkReleaseBuilds false
        warningsAsErrors true
    }
    detekt {
        profile('main') {
            input = "$projectDir/src/main/java"
            config = rootProject.file('detekt-config.yml')
            filters = '.*test.*,.*/resources/.*,.*/tmp/.*'
            output = "$projectDir/build/reports/detekt"
        }
    }
    ktlint {
        // android true
        includeVariants { variant -> variant.name.contains('debug') }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation "junit:junit:4.12"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation('org.jetbrains.spek:spek-api:1.1.5') {
        exclude group: 'org.jetbrains.kotlin'
    }
    testImplementation('org.jetbrains.spek:spek-junit-platform-engine:1.1.5')  {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
    testImplementation "org.junit.platform:junit-platform-runner:1.3.1"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

project.afterEvaluate {
    android.applicationVariants.all { variant ->
        def realVariantName = variant.name
        def variantName = variant.name.capitalize()

        def task = project.tasks["jacocoTest${variantName}UnitTestReport"]
        def defaultExcludes = task.classDirectories.excludes

        def excludes = defaultExcludes + ['**/R.class', '**/R$*.class', '**/com/android/**/*.*',
                                          '**/BuildConfig.class', '**/*Activity*.class', '**/DataBinder*.class',
                                          '**/*Fragment*.class', '**/*Receiver.class', '**/*Manifest*.class',
                                          '**/*Application*.class', 'android/**/*.*', '**/*$[0-9].*']

        def kotlinDebugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/${realVariantName}", excludes: excludes)

        task.classDirectories.excludes = excludes
        task.classDirectories += kotlinDebugTree
    }
}